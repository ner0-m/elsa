@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(Eigen3 3.3 NO_MODULE REQUIRED)
find_dependency(spdlog REQUIRED)
find_dependency(OpenMP QUIET)

set(_supported_components @ELSA_REGISTERED_COMPONENTS@)
set(_component_dependencies @ELSA_COMPONENTS_DEPENDENCIES@)

# if no specific components requested, include all
if(NOT elsa_FIND_COMPONENTS)
    foreach(_comp ${_supported_components})
        find_dependency(elsa_${_comp})
        include(${CMAKE_CURRENT_LIST_DIR}/elsa_${_comp}Targets.cmake)
    endforeach()
    message(STATUS "elsa found, using all modules")
else()
    foreach(_comp ${elsa_FIND_COMPONENTS})
        if (NOT _comp IN_LIST _supported_components)
            set(elsa_FOUND False)
            set(elsa_NOT_FOUND_MESSAGE "Unsupported component: ${_comp}")
        endif()
        find_dependency(elsa_${_comp})
        include(${CMAKE_CURRENT_LIST_DIR}/elsa_${_comp}Targets.cmake)
    endforeach()
    message(STATUS "elsa found, using components: ${elsa_FIND_COMPONENTS}")
endif()
