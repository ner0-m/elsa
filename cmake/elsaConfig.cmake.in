@PACKAGE_INIT@

message(STATUS "finding elsa dependencies...")

include(CMakeFindDependencyMacro)
find_dependency(OpenMP QUIET)
find_dependency(Threads REQUIRED)  # for spdlog

# when we bundle/git-download these libs
# we have to ensure that an elsa-downstream project uses the same version,
# and the path to this is encoded in our target info files.
set(BUNDLED_DEPS
    spdlog
    eigen3
)
foreach(DEP ${BUNDLED_DEPS})
    if(EXISTS "${CMAKE_CURRENT_LIST_DIR}/${DEP}Targets.cmake")
        # use the bundled version
        message(STATUS "using elsa-bundled version of dependency ${DEP}")
        include("${CMAKE_CURRENT_LIST_DIR}/${DEP}Targets.cmake")
    else()
        # use the system version
        message(STATUS "finding system-version of dependency ${DEP}...")
        find_dependency(${DEP} REQUIRED)
    endif()
endforeach()


if(EXISTS "@CMAKE_CUDA_COMPILER@")
    enable_language(CUDA)
    find_library(CUDART_LIBRARY cudart ${CMAKE_CUDA_IMPLICIT_LINK_DIRECTORIES})
endif()

if(@ELSA_BUILD_WITH_QUICKVEC@) # ELSA_BUILD_WITH_QUICKVEC
    find_dependency(quickvec REQUIRED)
endif()

set(_supported_components @ELSA_REGISTERED_COMPONENTS@)

if(NOT elsa_FIND_COMPONENTS)
    # no specific components requested, include all
    message(STATUS "finding elsa_all module")
    find_dependency(elsa_all REQUIRED HINTS ${CMAKE_CURRENT_LIST_DIR})
    if (elsa_all_FOUND)
        include(${CMAKE_CURRENT_LIST_DIR}/elsa_allTargets.cmake)
    endif()
    message(STATUS "elsa found, using all modules")
else()
    # Include specific components
    message(STATUS "finding requested elsa components")
    foreach(_comp ${elsa_FIND_COMPONENTS})
        message(STATUS "elsa loading ${_comp}")
        if(NOT _comp IN_LIST _supported_components)
            set(elsa_FOUND False)
            set(elsa_NOT_FOUND_MESSAGE "Unsupported component: ${_comp}")
        endif()

        find_dependency(elsa_${_comp} REQUIRED)
        if (elsa_${_comp}_FOUND)
            message(STATUS "Including ${_comp}")
            include(${CMAKE_CURRENT_LIST_DIR}/elsa_${_comp}Targets.cmake)
        endif()
    endforeach()
    message(STATUS "elsa found, using components: ${elsa_FIND_COMPONENTS}")
endif()

check_required_components(elsa)
