@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
find_dependency(Eigen3 3.3.7 REQUIRED)
find_dependency(spdlog REQUIRED)
find_dependency(OpenMP QUIET)

if (EXISTS "@CMAKE_CUDA_COMPILER@")
    message(STATUS "Enable CUDA")
    enable_language(CUDA)
    find_library(CUDART_LIBRARY cudart ${CMAKE_CUDA_IMPLICIT_LINK_DIRECTORIES})
endif()

if (@ELSA_BUILD_WITH_QUICKVEC@) # ELSA_BUILD_WITH_QUICKVEC
    message(STATUS "Looking for quickvec")
    find_dependency(quickvec REQUIRED)
endif()

set(_supported_components @ELSA_REGISTERED_COMPONENTS@)

# if no specific components requested, include all
if(NOT elsa_FIND_COMPONENTS)
    foreach(_comp ${_supported_components})
        include(${CMAKE_CURRENT_LIST_DIR}/elsa_${_comp}Targets.cmake)
    endforeach()
    message(STATUS "elsa found at ${CMAKE_CURRENT_LIST_DIR}")
    message(STATUS "elsa found, using all modules")
else()
    foreach(_comp ${elsa_FIND_COMPONENTS})
        if (NOT _comp IN_LIST _supported_components)
            set(elsa_FOUND False)
            set(elsa_NOT_FOUND_MESSAGE "Unsupported component: ${_comp}")
        endif()
        include(${CMAKE_CURRENT_LIST_DIR}/elsa_${_comp}Targets.cmake)
    endforeach()
    message(STATUS "elsa found, using components: ${elsa_FIND_COMPONENTS}")
endif()

check_required_components(elsa)
